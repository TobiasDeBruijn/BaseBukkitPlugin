import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

version = pluginVersion
group = 'dev.array21'	

repositories {
	mavenCentral()
	maven{ url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		filter ReplaceTokens, tokens: [version: version]
	}
}

dependencies {
	//Spigot API
	compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
}

task testJar(type: Jar) {
	description 'Build a test Jar'
	archiveClassifier = 'DEV'
	from sourceSets.main.output
	destinationDirectory = file("$rootDir/server/plugins")
}

task releaseJar(type: Jar) {
	description 'Build a release Jar'
	archiveClassifier = 'RELEASE'
	from sourceSets.main.output
	destinationDirectory = file("$rootDir/releases")
}

//This task is for GitHub Actions only
task ghActions(type: Jar) {	
	description 'GitHub Actions Task. Do not use in regular development!'
	version = ''
	archiveClassifier = ''
	archiveBaseName = 'output'
	
	from sourceSets.main.output
	destinationDirectory = file("$rootDir/actions")
}

